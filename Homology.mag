import "/Applications/Magma/package/Geometry/ModFrm/operators.m":
    get_Wq; // "Quick" computations of atkin lehner operators
            // It's actually slow af, if we can speed this up that would be
            // super great!
import "ModularFormLinearAlgebra.mag":
    BasisExpand,
    ChangeOfBasis,
    TransformBasis;

import "Helpers.mag":
    debugLog,
    SNFToOrdersAndBasis;

import "ModularForms.mag":
    Psi,
    ComputeC4C6Basis,
    ComputeQ2Q4Basis,
    q2,
    q4,
    Delta,
    c4,
    c6,
    IsModular,
    PsiDP1Inv,
    NormalizeForm,
    NormalizeFormWOThree;

// This computes the relations matrix for something to be in the kernel of (psid + 1 - phif)
KernelMatrix := function(weight : wq := -1)
    //"Computed M_" cat IntegerToString(weight) cat "(Gamma_0(2)) Space";
    FormSpace := BaseExtend(ModularForms(2,weight), Rationals());
    debugLog("KERMAT: Computing basis");
    basis := ComputeQ2Q4Basis(weight);
    //"Computed Basis";
    dim := #basis;
    //"Computed Dimension";
    half_weight := (2^(Truncate(weight/2)));
    //"Computed Half Weight";
    power_weight := half_weight*half_weight;
    //"Computed Weight";
    //This computation of the dual basis takes the longest amount of time!
    debugLog("KERMAT: Getting WQ");
    if wq eq -1 then
        wq := get_Wq(FormSpace, 2);
    end if;
    debugLog("KERMAT: Computing dual basis");
    dual_basis := [ Psi(f : wq := wq) : f in basis];
    //dual_basis := [];

    //"Computed dual of the basis!";
    CoefRing := PolynomialRing(loc<Integers() | 3>, dim); // Coefficient ring for x1 ... xDim
    //AssignNames(~CoefRing, CreateBasisString(dim));
    eqns := [];

    // We now attempt to get a system of equations using the constraining equation
    // 2^w * (f(tau) - f(2tau) - psi(f)(2tau)) + psi(f)(tau) = 0
    // Expanding f into the basis of q2q4 forms we represent f as 
    // f = sum x_i (q2q4)_i
    // psi(f) = sum x_i (psi(q2q4_i))
    //
    debugLog("KERMAT: Computing equations");
    for i in [0 .. dim-1] do // We need dim equations, which we get from the [0 .. dim - 1] degree q-expansion coefficients
        expr := 0; // We build up the i'th degree expression basis element by basis element
        for n in [1 .. dim] do // Loop over the basis elements
            f_n := basis[n]; //nth basis element
            var := CoefRing . n; // Represents the xi term in the basis expension
            dual_f_n := dual_basis[n];
            // We now get the following coefficients to compute
            ith_coef_f := CoefRing ! Coefficient(f_n, i); // f(tau)_i
            ith_coef_f_dual := CoefRing ! Coefficient(dual_f_n, i); // psi(f)(tau)_i
            i2th_coef_f := 0; // If i is odd, f(2tau)_i is zero
            i2th_coef_f_dual := 0; // similar to above
            if (i mod 2) eq 0 then
                half_i := Truncate(i/2);
                i2th_coef_f := CoefRing ! Coefficient(f_n, half_i); // f(2tau)_i
                i2th_coef_f_dual := CoefRing ! Coefficient(dual_f_n, half_i); // psi(f)(2tau)_i
            end if;
            sub_expr := var*(power_weight*(ith_coef_f
                            - i2th_coef_f
                            - i2th_coef_f_dual)
                            + ith_coef_f_dual); 
            // This represents x_i * (the constraining equation applied to (q2q4)_n at the i'th q expansion coefficient)
            expr := expr + sub_expr; // Build the expression up as a sum
        end for;
        eqns := Append(eqns, expr); // Once the expression is built up, add it to the matrix
    end for;
    //"Computed Equations";

    // This now parses the equations into matrix form, as all the terms are linear
    debugLog("KERMAT: Parsing eqns to matrix");
    mat_list := [];
    for eqn in eqns do
        coefs := [];
        for i in [1 .. dim] do
            var := CoefRing.i;
            coef := LeadingCoefficient(Coefficient(eqn, var, 1));
            coefs := Append(coefs, coef);
        end for;
        mat_list := Append(mat_list, coefs);
    end for;
    mat := Matrix(mat_list);
    return mat;
end function;

// This returns the basis for the Kernel at the given weight
Ker := function(weight : wq := -1) 
    debugLog("KER: Computing Kernel Matrix");
    mat := KernelMatrix(weight : wq := wq);
    mat := ChangeRing(mat, loc<Integers() | 3>);
    basis := ComputeQ2Q4Basis(weight);

    snfBasis := SNFToOrdersAndBasis(mat, basis);
    kerBasis := [];
    for pair in snfBasis do
        order := pair[1];
        //Only elements that vanish (i.e. in kernel) should be put into basis;
        if order eq 0 then
            Append(~kerBasis, pair[2]);
        end if;
    end for;
    return kerBasis;
end function;

// Determines if an element is in the kernel by checking if Psi(f) + f is modular
InKer := function(f : wq := -1)
    return IsModular(Psi(f : wq := wq)+f : wq := wq);
end function;

// Computes the (psi_d+1)^(-1)(c_ijk c4^i c6^j Delta^k) subbasis w/ me and Casey's work
KernelSubBasis := function(weight : wq := -1)
    if wq eq -1 then
        space := ModularForms(2, weight);
        wq := get_Wq(space, 2);
    end if;
    S2 := BaseExtend(ModularForms(2,2), Rationals());
    E22 := S2.1;

    S4 := BaseExtend(ModularForms(2,4), Rationals());
    E4P := S4.1;
    EI4 := S4.2;

    q2 := 1/2 * E22;
    q4 := 1/16*E4P - EI4;   

    c4 := 16*(q2^2 - 3*q4);
    c6 := 32*(9*q2*q4-2*q2^3);
    Delta := q4^2 * (16*q2^2 - 64*q4);
    basis := [];
    fourth := Truncate(weight/4); // c4's powers range from 0 to floor(weight / 4)
    sixth := Truncate(weight/6); // Computing c6's power requires access to 1/6
    debugLog("SubBasis: Beginning loop");
    for i in [0 .. fourth] do
        for j in [0 .. sixth] do
            pre_factor := 1;
            fac_1 := 1;
            fac_2 := 1;
            fac_3 := 1;
            remainder := weight - 4*i - 6*j;

            if (remainder mod 12) ne 0 or remainder lt 0 then
                continue;
            end if;
            if i ne 0 then // We need these if statements because MAGMA is weird about f^0
                fac_1 := c4^i;
            end if;
            if j ne 0 then 
                fac_2 := c6^j;
            end if;

            k := Truncate(remainder/12);
            if k ne 0 then
                fac_3 := Delta^k;
            end if;
            debugLog("SubBasis: Multiplying forms");
            form := fac_1 * fac_2 * fac_3;
            smallestCoef := LeadingCoefficient(qExpansion(form));
            formScaled := (1/smallestCoef) * form;
            coef_power := 1;
            if k ne 0 then 
                coef_power := Valuation(pAdicRing(3) ! weight) + 1;
            else
                coef_power := Maximum(0, Valuation(pAdicRing(3) ! weight) - Valuation(pAdicRing(3) ! i) - 1);
            end if;
            inInverseForm := 3^coef_power * formScaled;
            basis_form := PsiDP1Inv(inInverseForm : wq := wq);
            basis := Append(basis, basis_form);
        end for;
    end for;
    return basis;
end function;
// Denom seq for c4tilde powers: 1, 17, 91, 4369, 1271, 13981

// Computes the basis for the image of the map psi - 1: MF -> MF_0(2) 
// This just applies psi-1 to every basis element of MF.
PsiDM1Basis := function(weight : wq := -1)
    debugLog("IMAGE: Computing C4C6 Basis");
    basis := ComputeC4C6Basis(weight);
    power := Truncate(weight/2);
    debugLog("IMAGE: Computing Psi - 1 of basis");
    psi_minus := [(Psi(f : wq := wq) - f) : f in basis];
    return psi_minus;
end function;


// This function returns the homology group at a given weight w.
// Parameters: weight, an even integer >= 4 representing the weight of the
// forms we compute at.
// Returns: A pair of two lists, each of which have elements of the form
//          <order, generator> representing the fact that the group is Z_3 / order Z_3 generated
//          by generator.
//          The first of the pair is for the MF0(2) factor, and the second part is for the MF factor.
Homology := function(weight : wq := -1)
    if wq eq -1 then
        debugLog("HOMOLOGY: Calculating wq");
        space := ModularForms(2, weight);
        wq := get_Wq(space, 2);
    end if;
    debugLog("HOMOLOGY: Calculating kernel basis");
    kerBasis := Ker(weight : wq := wq);
    psidBasis := [];
    for f in kerBasis do
        Append(~psidBasis, Psi(f : wq := wq) + f);
    end for;

    debugLog("HOMOLOGY: Calculating image basis");
    imBasis1 := H1ImageBasis(weight : wq := wq);


    debugLog("HOMOLOGY: Calculating COB Matrix");
    M1 := ChangeOfBasis(kerBasis, imBasis1);
     
    R := loc<Integers() | 3>;
    M1 := ChangeRing(M1, R);

    debugLog("HOMOLOGY: Calculating SNF of COB Mat");

    l1 := SNFToOrdersAndBasis(M1, kerBasis);
    
    return l1;
end function;

// This computes the basis of the image of the map \psi_d+1 - \phi f: MF_0(2) \oplus MF -> MF_0(2)
// Again this just takes basis elements of the parts, applies the relevant maps, and joins them up in a single basis.
CokerImageBasis := function(k : wq := -1)
    B1 := ComputeQ2Q4Basis(k);
    B2 := ComputeC4C6Basis(k);

    imageBasis := [];
    for f in B1 do
        // First part is psi_d(f)+f for f in MF_0(2)
        Append(~imageBasis, Psi(f : wq := wq) + f);
    end for;

    for g in B2 do
        // Second part is phi_f(g) = g for g in MF
        Append(~imageBasis, g);
    end for;


    // The following code is just to make sure that the 
    // basis given is not only spanning, but linearly independent.
    COB := ChangeOfBasis(B1, imageBasis);
    COB := ChangeRing(COB, loc<Integers() | 3>);

    EF := EchelonForm(COB);
    indep_basis := [];

    rows := NumberOfRows(EF);
    cols := NumberOfColumns(EF);
    dim := Minimum([rows, cols]);
    for i in [1 .. dim] do
        if EF[i,i] ne 0 then
            Append(~indep_basis, imageBasis[i]);
        end if;
    end for;

    return indep_basis;
end function;

// Computes MF_0(2)/(im psi + 1 - phi_f) at a weight k
Coker := function(k : wq := -1)
    B1 := ComputeQ2Q4Basis(k);
    B2 := CokerImageBasis(k);

    COB := ChangeOfBasis(B1, B2);
    snf := SNFToOrdersAndBasis(COB, B1);

    return snf;
end function;

// Computes MF_0(2) / (im psi_d+1) at weight k
function ModPsiDP1(k : wq := -1)
    B1 := ComputeQ2Q4Basis(k);
    imageBasis := [Psi(f : wq := wq) + f : f in B1];

    COB := ChangeOfBasis(B1, imageBasis);

    snf := SNFToOrdersAndBasis(COB, B1);
    return snf;
end function;

// Computes (im psi_d+1 - \phi_f) / (im \psi_d+1).
function CokerImageModPsiDP1(k : wq := -1)
    b1 := CokerImageBasis(k : wq := wq);
    b2 := [Psi(f : wq := wq) + f : f in ComputeQ2Q4Basis(k)];

    COB := ChangeOfBasis(b1, b2);

    return SNFToOrdersAndBasis(COB, b1);
end function;

// Returns the form that generates homology at a given weight w.
function HomologyGenerator(w : wq := -1)
    f := 1;
    if w mod 4 eq 0 then
        f := c4^(Truncate(w/4));
    elif w mod 6 eq 0 then
        f := c6^(Truncate(w/6));
    elif w mod 6 eq 2 then
        sixPow := Truncate(w/6)-1;
        fac1 := 1;
        if sixPow ne 0 then
            fac1 := c6^sixPow;
        end if;
        f := fac1 * c4^2;
    else
        f := c6^(Truncate(w/6)) * c4;
    end if;

    return PsiDP1Inv(f : wq := wq);
end function;